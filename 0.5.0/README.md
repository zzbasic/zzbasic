# ZzBasic - Documenta√ß√£o v0.5.0

![ZzBasic](zzbasic_logo1.png)

**ZzBasic** √© uma linguagem de programa√ß√£o interpretada que combina a simplicidade do BASIC cl√°ssico com recursos modernos. √â um projeto solo desenvolvido com cuidado e aten√ß√£o aos detalhes.

## Vers√£o Atual: **0.5.0**

### üöÄ Caracter√≠sticas

- ‚úÖ **Sintaxe limpa** - Comandos em caixa baixa (`let`, n√£o `LET`)
- ‚úÖ **Tipagem din√¢mica** - N√∫meros e strings
- ‚úÖ **REPL interativo avan√ßado** - Com comandos de debug e an√°lise
- ‚úÖ **Execu√ß√£o de arquivos** - Scripts com extens√£o `.zz`
- ‚úÖ **M√∫ltiplos statements** - Separe comandos com `:`, `;` ou nova linha
- ‚úÖ **Verifica√ß√£o de tipos** - Erros claros em opera√ß√µes inv√°lidas
- ‚úÖ **Gerenciamento de mem√≥ria** - Sem vazamentos
- ‚úÖ **An√°lise l√©xica e AST** - Ferramentas de debug integradas
- ‚úÖ **Comando `print`** - Sa√≠da formatada com controle expl√≠cito de linha
- ‚úÖ **Atalho `?`** - Alternativa r√°pida para `print`
- ‚úÖ **Cores no `print`** - Suporte a 24 cores (normais, brilhantes e fundo)
- ‚úÖ **Largura de campo(width) e alinhamento(alignment) no `print`** 
- ‚úÖ **Comando `input`** - Entrada de dados do usu√°rio
- ‚úÖ **Tipo booleano** - `true` e `false`

---
![zzbasic_repl](zzbasic_0_5_0.png)
---

### üìã Hist√≥rico de Vers√µes

| Vers√£o | Status | Recursos Principais |
|--------|--------|-------------------|
| 0.1.0 | Lan√ßada | Express√µes matem√°ticas b√°sicas |
| 0.2.0 | Lan√ßada | Vari√°veis (`let`), strings, tabela de s√≠mbolos |
| 0.3.0 | Lan√ßada | M√∫ltiplos statements, REPL avan√ßado, comandos de debug |
| 0.4.0 | Lan√ßada | Comando `print`, controle de linha, sa√≠da formatada |
| 0.4.1 | Lan√ßada | Comando `print` com cores |
| 0.4.2 | Lan√ßada | Largura de campo e alinhamento no `print` |
| 0.4.3 | Lan√ßada | `input` |
| 0.5.0 | Lan√ßada | tipo booleano |
| 0.5.1 | **em desenvolvimento** | operadores de compara√ß√£o |

---

## üì• Instala√ß√£o e Compila√ß√£o

### Pr√©-requisitos

- **Compilador C**: GCC, Clang ou MSVC
- **Make** (opcional)
- **Git** (para clonar)

### Compila√ß√£o (Linux/macOS)

```bash
# Clone o reposit√≥rio
git clone https://github.com/zzbasic/zzbasic.git
cd zzbasic/0.4.3

# Compile
make

# Execute
./zzbasic
```

### Compila√ß√£o (Windows)

```bash
# Com MinGW
cd zzbasic\0.4.3
gcc -o zzbasic *.c
zzbasic.exe
```

---

## üìö Refer√™ncia da Linguagem

### Gram√°tica EBNF (v0.5.0)

```
=====================================================================
ZzBasic - GRAM√ÅTICA v0.5.0 
Estruturas de Controle: if, else if, else
√öltima atualiza√ß√£o: 20260402
=====================================================================

=====================================================================
PROGRAMA ESTRUTURAL 
=====================================================================
program             := statement_list

statement_list      := statement (EOL statement)* EOL?

statement       := assignment_stmt
                | print_stmt
                | color_stmt 
                | input_stmt 
                | if_stmt
                | expression_stmt


=====================================================================
assignment_stmt 
=====================================================================
assignment_stmt := 'let' IDENTIFIER '=' expression


=====================================================================
print_stmt     
=====================================================================
print_stmt      := ('print' | '?') print_item* ('nl' | EOL | EOF)

print_item      := expression 
                | format_directive
                | color_directive

format_directive := alignment
                  | width_spec

alignment       := 'left' | 'right' | 'center'

width_spec      := 'width' '(' NUMBER ')'

color_directive := color_name 
                | 'nocolor'

color_name      := normal_color
                | bright_color
                | background_color

normal_color    := 'black' | 'red' | 'green' | 'yellow'
                | 'blue' | 'magenta' | 'cyan' | 'white'

bright_color    := 'bred' | 'bgreen' | 'byellow' | 'bblue'
                | 'bmagenta' | 'bcyan' | 'bwhite'

background_color := 'bgblack' | 'bgred' | 'bggreen' | 'bgyellow'
                  | 'bgblue' | 'bgmagenta' | 'bgcyan' | 'bgwhite'



=====================================================================
color_stmt 
=====================================================================
color_stmt := COLOR_TOKEN


=====================================================================
input_stmt
=====================================================================
input_stmt      := 'input' (STRING)? IDENTIFIER  


=====================================================================
if_stmt
=====================================================================
if_stmt         := 'if' logical_expr 'then' EOL   
                       statement*
                   else_if_part*
                   else_part?
                   'end' 'if' EOL

else_if_part    := 'else' 'if' logical_expr 'then' EOL
                       statement*

else_part       := 'else' EOL
                       statement*


=====================================================================
expression_stmt 
=====================================================================
expression_stmt  := logical_expr

logical_expr     := logical_or_expr

logical_or_expr  := logical_and_expr ('or' logical_and_expr)*

logical_and_expr := comparison_expr ('and' comparison_expr)*

comparison_expr  := expression (comparison_op expression)*

comparison_op    := '==' | '!=' | '<' | '>' | '<=' | '>='


=====================================================================
expression 
=====================================================================
expression       := term (('+' | '-') term)*

term             := factor (('*' | '/') factor)*

factor           := ('+' | '-'| 'not' | '!')? atom

atom             := NUMBER | STRING | BOOLEAN | IDENTIFIER | '(' expression ')' 


=====================================================================
LITERAIS 
=====================================================================
COMMENT         := '#' ~[\n\r]*

IDENTIFIER      := [a-zA-Z_][a-zA-Z0-9_]*

NUMBER          := [0-9]+ ('.' [0-9]+)?

STRING          := '"' [^"]* '"'

BOOLEAN         := 'true' | 'false'


=====================================================================
PALAVRAS-CHAVE 
=====================================================================
let
print
input
if
then
else
end
and
or
not
nl

black
red
green
yellow
blue
magenta
cyan
white

bred
bgreen
byellow
bblue
bmagenta
bcyan
bwhite

bgblack
bgred
bggreen
bgyellow
bgblue
bgmagenta
bgcyan
bgwhite


=====================================================================
OPERADORES 
=====================================================================
PLUS            := '+'
MINUS           := '-'
MULT            := '*'
DIV             := '/'
ASSIGN          := '='
EQUAL           := '='
NOT_EQUAL       := '!='
LESS            := '<'
GREATER         := '>'
LESS_EQUAL      := '<='
GREATER_EQUAL   := '>='
LPAREN          := '('
RPAREN          := ')'

AND             := 'and'
OR              := 'or' 
NOT             := 'not' | !'


=====================================================================
HIERARQUIA DE PRECED√äNCIA (do menor para o MAIOR)
=====================================================================
N√≠vel   Operador                Associatividade     Fun√ß√£o do Parser
1       print/?, input, nl, if  -                   cada recurso tem a sua    
2       =                       -                   parse_assignment_stmt()
3       or                      Esquerda            parse_logical_or_expr()
4       and                     Esquerda            parse_logical_and_expr()
5       ==, !=, <, >, <=, >=    Esquerda            parse_comparison_expr()
6       +, -                    Esquerda            parse_expression()
7       *, /                    Esquerda            parse_term()
8       +, -, not, ! (un√°rios)  Direita             parse_factor()
9       √Åtomos, par√™nteses      -                   parse_atom()


=====================================================================
NOTAS IMPORTANTES SOBRE O PRINT 
=====================================================================
# 1. 'nl' √© OPCIONAL no final do print
# 2. Comportamento padr√£o: acumula na mesma linha
# 3. Com 'nl' no final: quebra linha ap√≥s imprimir
# 4. Espa√ßo √© separador padr√£o entre print_items


=====================================================================
NOTAS SOBRE IF...ELSE
=====================================================================
# 1. IF sempre requer THEN
# 2. ELSE IF √© opcional e pode ser encadeado
# 3. ELSE √© opcional e deve ser o √∫ltimo bloco
# 4. IF sempre termina com END IF
# 5. Cada bloco (IF, ELSE IF, ELSE) cont√©m zero ou mais statements


FIM DA GRAM√ÅTICA DO ZzBasic v0.5.0
=====================================================================

```

---

## üöÄ Pr√≥ximas Vers√µes

### v0.5.1 
- Operadores de compara√ß√£o e l√≥gicos ('=', '<>', '<', '>', '<=', '>=', AND, OR, NOT)

---

## üìñ Documenta√ß√£o Adicional

Para mais detalhes, consulte o **Manual do Usu√°rio** (em desenvolvimento).

---

## ü§ù Contribui√ß√µes

ZzBasic √© um projeto solo, mas sugest√µes e feedback s√£o bem-vindos!

---

## üìÑ Licen√ßa

MIT License

Copyright (c) 2026 arataca89

---

**√öltima atualiza√ß√£o:** Fevereiro de 2026
**Vers√£o:** 0.5.0

